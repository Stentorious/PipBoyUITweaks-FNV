int iSlotMask
int iType
float fWeapWg
float fWeapVal
float fWeapStr
float fBodyWg
float fBodyVal
float fEyesWg
float fEyesVal
float fHeadWg
float fHeadVal
ref rItem
array_var aEquippedItems
array_var aForE

begin Function {}

	; Cache equipped item stats
	aEquippedItems = Player.GetEquippedItemRefs
	if aEquippedItems
		foreach aForE <- aEquippedItems
			rItem = *aForE
			if eval IsFormValid rItem
				iSlotMask = rItem.GetESM
				iType = rItem.GetEquipType

				; Weapon
				if eval rItem.GetType == 40
					fWeapWg = Player.GetCalculatedEquippedWeight 0 32
					fWeapVal = rItem.GetCalculatedItemValue
					fWeapStr = GetMaxOf 0 ((rItem.GetReqStr) - Player.GetPerkModifier 53 0 rItem)

				; Armor (Body)
				elseif eval GetBit iSlotMask 2
					fBodyWg = rItem.GetWeight
					fBodyVal = rItem.GetCalculatedItemValue

				; Armor (Eyes)
				elseif iSlotMask == 18432
					fEyesWg = rItem.GetWeight
					fEyesVal = rItem.GetCalculatedItemValue

				; Armor (Head)
				elseif iType == 8 && GetBit iSlotMask 11 != 1
					fHeadWg = rItem.GetWeight
					fHeadVal = rItem.GetCalculatedItemValue

				; Armor (Eyes + Head)
				elseif iType == 8
					fHeadWg = fEyesWg = rItem.GetWeight
					fHeadWg = fEyesVal = rItem.GetCalculatedItemValue
				endif
			endif
		loop
	endif

	; "Modify Light Items" perk modifier
	if eval rItem.GetType == 24
		if eval fBodyWg <= 2 && Player.GetPerkModifier 55 0 == 1
			fBodyWg /= 2
		endif
		if eval fEyesWg <= 2 && Player.GetPerkModifier 55 0 == 1
			fEyesWg /= 2
		endif
		if eval fHeadWg <= 2 && Player.GetPerkModifier 55 0 == 1
			fHeadWg /= 2
		endif
	endif

	; "bWeightlessItems" lStewieAl Tweak
	if eval Goo1.AuxVarGetFlt "*Tweaks:bWeightlessItems" 0 == 1
		if eval iType == 40 ; Weapon
			fWeapWg *= Goo1.AuxVarGetFlt "*Tweaks:bWeightlessItems" 5
		elseif eval iType == 24 ; Armor
			fBodyWg *= Goo1.AuxVarGetFlt "*Tweaks:bWeightlessItems" 3
			fEyesWg *= Goo1.AuxVarGetFlt "*Tweaks:bWeightlessItems" 3
			fHeadWg *= Goo1.AuxVarGetFlt "*Tweaks:bWeightlessItems" 3
		endif
	endif

	Goo1.AuxVarSetFlt "*PUI_Stat" fWeapStr 0
	Goo1.AuxVarSetFlt "*PUI_Stat" fWeapWg 1
	Goo1.AuxVarSetFlt "*PUI_Stat" fWeapVal 2
	Goo1.AuxVarSetFlt "*PUI_Stat" fBodyWg 3
	Goo1.AuxVarSetFlt "*PUI_Stat" fBodyVal 4
	Goo1.AuxVarSetFlt "*PUI_Stat" fEyesWg 5
	Goo1.AuxVarSetFlt "*PUI_Stat" fEyesVal 6
	Goo1.AuxVarSetFlt "*PUI_Stat" fHeadWg 7
	Goo1.AuxVarSetFlt "*PUI_Stat" fHeadVal 8

	aEquippedItems = aForE = ar_null

end