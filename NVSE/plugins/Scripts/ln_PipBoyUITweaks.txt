; Pip-Boy UI Tweaks - Stentorious

; Aux Var
; "*PipTweaks_INI"
; 0 = Extended stat comparisons
; 1 = Weapon mod menu enhanced
; 2 = Weapon mod menu accurate descriptions
; 3 = Colorful stat comparisons
; 4 = Colorful encumbrance
; 5 = Colorful skill effects
; 6 = Colorful Special effects
; 7 = Colorful hit points effects
; 8 = Colorful paper doll
; 9 = Colorful RAD
; 10 = Colorful H2O
; 11 = Colorful FOD
; 12 = Colorful SLP
; 13 = Crit Info item card
; 14 = ySI ammo icon
; 15 = Expanded menu support

; Update player stats
SetUIFloatAlt "HUDMainMenu\_PlrStat+Encumbrance" (IsPCOverencumbered)
if eval Goo1.AuxVarGetFlt "*PipTweaks_Init"
	float fAVPercent = 1.0
	if eval (float fBaseValue = Player.GetPermanentActorValue ActionPoints) > 0
		fAVPercent = (Player.GetAV ActionPoints) / fBaseValue
	endif
	SetUIFloatAlt "HUDMainMenu\_PlrStat+ActionPointPer" fAVPercent
	fAVPercent = 1.0
	if eval (fBaseValue = Player.GetPermanentActorValue Health) > 0
		fAVPercent = (Player.GetAV Health) / fBaseValue
	endif
	SetUIFloatAlt "HUDMainMenu\_PlrStat+HealthPer" fAVPercent
	SetUIFloatAlt "HUDMainMenu\_PlrStat+FOD" (Player.GetAV Hunger)
	SetUIFloatAlt "HUDMainMenu\_PlrStat+H2O" (Player.GetAV Dehydration)
	SetUIFloatAlt "HUDMainMenu\_PlrStat+SLP" (Player.GetAV SleepDeprevation)
	SetUIFloatAlt "HUDMainMenu\_PlrStat+RAD" (Player.GetAV RadiationRads)
	SetUIFloatAlt "HUDMainMenu\_Setting+Hardcore" (Player.IsHardcore)
	return
endif
Goo1.AuxVarSetFlt "*PipTweaks_Init" 1

; On game restart
; Check for requirements
if eval GetNVSEVersionFull < 6.40
	MessageBoxEx "Pip-Boy UI Tweaks missing requirement!%rInstall xNVSE 6.4.0+."
	return
endif
if GetPluginVersion "JohnnyGuitarNVSE" < 508
	MessageBoxEx "Pip-Boy UI Tweaks missing requirement!%rInstall latest JohnnyGuitar NVSE plugin."
	return
endif
if GetPluginVersion "ShowOffNVSE Plugin" < 182
	MessageBoxEx "Pip-Boy UI Tweaks missing requirement!%rInstall latest ShowOff NVSE plugin."
	return
endif
if GetPluginVersion "UI Organizer Plugin" < 230
	MessageBoxEx "Pip-Boy UI Tweaks missing requirement!%rInstall latest User Interface Organizer plugin."
	return
endif
if GetPluginVersion "lStewieAl's Tweaks" < 960
	MessageBoxEx "Pip-Boy UI Tweaks missing requirement!%rInstall latest lStewieAl's Tweaks."
	return
endif
if eval GetPluginVersion "yUI" >= 140 && FileExists "menus\ySI\ySI.xml"
	Goo1.AuxVarSetFlt "*_ySI-SortingIcons" 1
endif

; Check for mod updates
if eval FileExists "NVSE\Plugins\Scripts\ln_ItemCards.txt" && FileExists "Menus\Prefabs\Stentorious\ItemCards\Main.xml" == 0
	MessageBoxEx "Pip-Boy UI Tweaks detected outdated mod!%r%rInstall latest Item Cards."
endif
if eval FileExists "NVSE\Plugins\Scripts\ln_PipBoyModVendor.txt" && FileExists "Menus\Prefabs\Stentorious\PipBoyModVendor\CapsInfo.xml" == 0
	MessageBoxEx "Pip-Boy UI Tweaks detected outdated mod!%r%rInstall latest Pip-Boy Weapon Mod Vendor."
endif
if eval FileExists "NVSE\Plugins\Scripts\ln_VendorRadio.txt" && FileExists "Menus\Prefabs\Stentorious\VendorRadio\Dummy.xml" == 0
	MessageBoxEx "Pip-Boy UI Tweaks detected outdated mod!%r%rInstall latest Vendor Radio."
endif
if eval FileExists "NVSE\Plugins\Scripts\ln_PipBoyCasino.txt" && FileExists "Menus\Prefabs\Stentorious\PipBoyCasino\Dummy.xml" == 0
	MessageBoxEx "Pip-Boy UI Tweaks detected outdated mod!%r%rInstall latest Pip-Boy Casino."
endif

; Update player stats
SetUIFloatAlt "HUDMainMenu/_PlrStat+RAD1" (GetHardcoreStageThreshold Rad1)
SetUIFloatAlt "HUDMainMenu/_PlrStat+RAD2" (GetHardcoreStageThreshold Rad2)
SetUIFloatAlt "HUDMainMenu/_PlrStat+RAD3" (GetHardcoreStageThreshold Rad3)
SetUIFloatAlt "HUDMainMenu/_PlrStat+RAD4" (GetHardcoreStageThreshold Rad4)
SetUIFloatAlt "HUDMainMenu/_PlrStat+RAD5" (GetHardcoreStageThreshold Rad5)
SetUIFloatAlt "HUDMainMenu/_PlrStat+H2O1" (GetHardcoreStageThreshold Water1)
SetUIFloatAlt "HUDMainMenu/_PlrStat+H2O2" (GetHardcoreStageThreshold Water2)
SetUIFloatAlt "HUDMainMenu/_PlrStat+H2O3" (GetHardcoreStageThreshold Water3)
SetUIFloatAlt "HUDMainMenu/_PlrStat+H2O4" (GetHardcoreStageThreshold Water4)
SetUIFloatAlt "HUDMainMenu/_PlrStat+H2O5" (GetHardcoreStageThreshold Water5)
SetUIFloatAlt "HUDMainMenu/_PlrStat+FOD1" (GetHardcoreStageThreshold Hunger1)
SetUIFloatAlt "HUDMainMenu/_PlrStat+FOD2" (GetHardcoreStageThreshold Hunger2)
SetUIFloatAlt "HUDMainMenu/_PlrStat+FOD3" (GetHardcoreStageThreshold Hunger3)
SetUIFloatAlt "HUDMainMenu/_PlrStat+FOD4" (GetHardcoreStageThreshold Hunger4)
SetUIFloatAlt "HUDMainMenu/_PlrStat+FOD5" (GetHardcoreStageThreshold Hunger5)
SetUIFloatAlt "HUDMainMenu/_PlrStat+SLP1" (GetHardcoreStageThreshold Sleep1)
SetUIFloatAlt "HUDMainMenu/_PlrStat+SLP2" (GetHardcoreStageThreshold Sleep2)
SetUIFloatAlt "HUDMainMenu/_PlrStat+SLP3" (GetHardcoreStageThreshold Sleep3)
SetUIFloatAlt "HUDMainMenu/_PlrStat+SLP4" (GetHardcoreStageThreshold Sleep4)
SetUIFloatAlt "HUDMainMenu/_PlrStat+SLP5" (GetHardcoreStageThreshold Sleep5)

; Load INI config
Goo1.AuxVarSetFlt "*PipTweaks_INI" (eval GetINIFloat_Cached "InventoryMenu:bExtendStatComparisons" "Stentorious\PipBoyUITweaks.ini" 1) 0
Goo1.AuxVarSetFlt "*PipTweaks_INI" (eval GetINIFloat_Cached "InventoryMenu:bAddictionInfo" "Stentorious\PipBoyUITweaks.ini" 1) 1
Goo1.AuxVarSetFlt "*PipTweaks_INI" (eval GetINIFloat_Cached "WeaponModMenu:bAccurateDescriptions" "Stentorious\PipBoyUITweaks.ini" 1) 2
Goo1.AuxVarSetFlt "*PipTweaks_INI" (eval GetINIFloat_Cached "Color:bItemStatComparisons" "Stentorious\PipBoyUITweaks.ini" 1) 3

; UI traits
SetUIFloatAlt "HUDMainMenu\_PipTweaks+ExtMenuSupport" (eval GetINIFloat_Cached "General:bExtendMenuSupport" "Stentorious\PipBoyUITweaks.ini" 1)
SetUIFloatAlt "HUDMainMenu\_PipTweaks+ShiftItems" (eval GetINIFloat_Cached "InventoryMenu:bShiftUnequippable" "Stentorious\PipBoyUITweaks.ini" 1)
SetUIFloatAlt "HUDMainMenu\_PipTweaks+SkillInfo" (eval GetINIFloat_Cached "InventoryMenu:bSkillInfo" "Stentorious\PipBoyUITweaks.ini" 1)
SetUIFloatAlt "HUDMainMenu\_PipTweaks+CritInfo" (eval GetINIFloat_Cached "InventoryMenu:bCritInfo" "Stentorious\PipBoyUITweaks.ini" 1)
SetUIFloatAlt "HUDMainMenu\_PipTweaks+SearchIcon" (eval GetINIFloat_Cached "General:bSearchIcon" "Stentorious\PipBoyUITweaks.ini" 1)
SetUIFloatAlt "HUDMainMenu\_PipTweaks+WeapModUI" (eval GetINIFloat_Cached "WeaponModMenu:bWeaponStatUI" "Stentorious\PipBoyUITweaks.ini" 1)
SetUIFloatAlt "HUDMainMenu\_PipTweaks+HardcoreInfo" (eval GetINIFloat_Cached "InventoryMenu:bHardcoreInfo" "Stentorious\PipBoyUITweaks.ini" 1)
SetUIFloatAlt "HUDMainMenu\_PipTweaks+SeparateDRDT" (eval GetINIFloat_Cached "InventoryMenu:bSeparateDTDR" "Stentorious\PipBoyUITweaks.ini" 1)
SetUIFloatAlt "HUDMainMenu\_PipTweaks+NumericCND" (eval GetINIFloat_Cached "InventoryMenu:bNumericCondition" "Stentorious\PipBoyUITweaks.ini" 1)

; Color
SetUIFloatAlt "HUDMainMenu\_PipTweaks+Color_HP" (eval GetINIFloat_Cached "Color:bHitPoints" "Stentorious\PipBoyUITweaks.ini" 1)
SetUIFloatAlt "HUDMainMenu\_PipTweaks+Color_AP" (eval GetINIFloat_Cached "Color:bActionPoints" "Stentorious\PipBoyUITweaks.ini" 1)
SetUIFloatAlt "HUDMainMenu\_PipTweaks+Color_WG" (eval GetINIFloat_Cached "Color:bEncumbrance" "Stentorious\PipBoyUITweaks.ini" 1)
SetUIFloatAlt "HUDMainMenu\_PipTweaks+Color_StatMod" (eval GetINIFloat_Cached "Color:bStatModifier" "Stentorious\PipBoyUITweaks.ini" 1)
SetUIFloatAlt "HUDMainMenu\_PipTweaks+Color_Limb" (eval GetINIFloat_Cached "Color:bCrippledLimbs" "Stentorious\PipBoyUITweaks.ini" 1)
SetUIFloatAlt "HUDMainMenu\_PipTweaks+Color_RAD" (eval GetINIFloat_Cached "Color:bRADMeter" "Stentorious\PipBoyUITweaks.ini" 1)
SetUIFloatAlt "HUDMainMenu\_PipTweaks+Color_H2O" (eval GetINIFloat_Cached "Color:bH2OMeter" "Stentorious\PipBoyUITweaks.ini" 1)
SetUIFloatAlt "HUDMainMenu\_PipTweaks+Color_FOD" (eval GetINIFloat_Cached "Color:bFODMeter" "Stentorious\PipBoyUITweaks.ini" 1)
SetUIFloatAlt "HUDMainMenu\_PipTweaks+Color_SLP" (eval GetINIFloat_Cached "Color:bSLPMeter" "Stentorious\PipBoyUITweaks.ini" 1)
SetUIFloatAlt "HUDMainMenu\_PipTweaks+Color_CND" (GetINIFloat_Cached "Color:iCNDMeter" "Stentorious\PipBoyUITweaks.ini" 1)

; lStewieAl's Tweaks Auxiliary-Variables
Goo1.AuxVarSetFlt "*_Tweaks:bIndividialItemStats" (StewVal "bIndividialItemStats:Tweaks") 0
if eval FileExists "nvse\plugins\scripts\ln_ItemCards.txt" == 0

	; "bCritChanceIgnoresFireRate"
	Goo1.AuxVarSetFlt "*_Tweaks:bCritChanceIgnoresFireRate" (StewVal "bCritChanceIgnoresFireRate:Tweaks") 0

	; "bScaleCriticalDamage"
	Goo1.AuxVarSetFlt "*_Tweaks:bScaleCriticalDamage" (StewVal "bScaleCriticalDamage:Tweaks") 0
	if eval Goo1.AuxVarGetFlt "*_Tweaks:bScaleCriticalDamage" 0
		Goo1.AuxVarSetFlt "*_Tweaks:bScaleCriticalDamage" (StewVal "fCriticalDamageMult:Critical Hits") 1
	else
		Goo1.AuxVarSetFlt "*_Tweaks:bScaleCriticalDamage" 1 1
	endif

	; "bWeightlessItems"
	if eval StewVal "bWeightlessItems:Tweaks"
		Goo1.AuxVarSetFlt "*_Tweaks:bWeightlessItems" 1 0
		Goo1.AuxVarSetFlt "*_Tweaks:bWeightlessItems" (1 - (StewVal "bAid:Weightless Items")) 1
		Goo1.AuxVarSetFlt "*_Tweaks:bWeightlessItems" (1 - (StewVal "bAmmo:Weightless Items")) 2
		Goo1.AuxVarSetFlt "*_Tweaks:bWeightlessItems" (1 - (StewVal "bArmor:Weightless Items")) 3
		Goo1.AuxVarSetFlt "*_Tweaks:bWeightlessItems" (1 - (StewVal "bMisc:Weightless Items")) 4
		Goo1.AuxVarSetFlt "*_Tweaks:bWeightlessItems" (1 - (StewVal "bWeapons:Weightless Items")) 5
	endif

endif

; UI mod support flags
Goo1.AuxVarSetFlt "*_DarNifiedUINV.esp" (eval IsModLoaded "DarNifiedUINV.esp")
Goo1.AuxVarSetFlt "*_Vanilla UI Plus.esp" (eval IsModLoaded "Vanilla UI Plus.esp")
Goo1.AuxVarSetFlt "*_Vanilla UI Extension" (eval FileExists "textures\VUIE\Brackets\Bracket.dds")

; Stat comparison weapon\armor flag
Goo1.AuxVarSetFlt "*PipTweaks_FLAG" 1

; Init alpha pulse
SetUIFloatGradual "HUDMainMenu\_PipTweaks+Pulse" -204 0 1.5 2

; Match ItemModMenu font with InventoryMenu
SetUIFloatAlt "ItemModMenu\font" (GetUIFloatAlt "InventoryMenu\font")

; Enable lStewieAl's Tweaks search buttons
SetUIFloatAlt "StatsMenu\SM_SearchBar\_OffsetX" (GetUIFloatAlt "InventoryMenu\IM_SearchBar\_OffsetX")
SetUIFloatAlt "StatsMenu\SM_SearchBar\_OffsetY" (GetUIFloatAlt "InventoryMenu\IM_SearchBar\_OffsetY")
SetUIFloatAlt "MapMenu\MM_SearchBar\_OffsetX" (GetUIFloatAlt "InventoryMenu\IM_SearchBar\_OffsetX")
SetUIFloatAlt "MapMenu\MM_SearchBar\_OffsetY" (GetUIFloatAlt "InventoryMenu\IM_SearchBar\_OffsetY")

; ySI - Sorting Icons support
if eval Goo1.AuxVarGetFlt "*_ySI-SortingIcons"

	SetUIFloatAlt "HUDMainMenu\_PipTweaks+ySI_Font" (GetUIFloatAlt "InventoryMenu\GLOW_BRANCH\IM_MainRect\IM_ButtonRect\IM_EquipButton\button_text\font")
	SetUIFloatAlt "HUDMainMenu\_PipTweaks+ySI_Button" (eval GetINIFloat_Cached "ySI:bButtonIcons" "Stentorious\PipBoyUITweaks.ini" 1)
	SetUIFloatAlt "HUDMainMenu\_PipTweaks+ySI_Resist" (eval GetINIFloat_Cached "ySI:bResistanceIcons" "Stentorious\PipBoyUITweaks.ini" 1)
	SetUIFloatAlt "HUDMainMenu\_PipTweaks+ySI_WGInfo" (eval GetINIFloat_Cached "ySI:bWGIcon" "Stentorious\PipBoyUITweaks.ini" 1)
	SetUIFloatAlt "HUDMainMenu\_PipTweaks+ySI_CapsInfo" (eval GetINIFloat_Cached "ySI:bCapsIcon" "Stentorious\PipBoyUITweaks.ini" 1)
	SetUIFloatAlt "HUDMainMenu\_PipTweaks+ySI_WeightInfo" (eval GetINIFloat_Cached "ySI:bWeightInfoIcon" "Stentorious\PipBoyUITweaks.ini" 1)
	SetUIFloatAlt "HUDMainMenu\_PipTweaks+ySI_ValueInfo" (eval GetINIFloat_Cached "ySI:bValueInfoIcon" "Stentorious\PipBoyUITweaks.ini" 1)
	SetUIFloatAlt "HUDMainMenu\_PipTweaks+ySI_AmmoInfo" (eval GetINIFloat_Cached "ySI:bAmmoInfoIcon" "Stentorious\PipBoyUITweaks.ini" 1)

	; ySI icons (Buttons)
	SetUIStringAlt "StatsMenu\GLOW_BRANCH\stats_status_container\stats_stimpak_button\ySIImage\filename" "interface\icons\Aid_Stimpak.dds"
	SetUIStringAlt "StatsMenu\GLOW_BRANCH\stats_status_container\stats_drbag_button\ySIImage\filename" "interface\icons\Aid_DoctorBag.dds"
	SetUIStringAlt "StatsMenu\GLOW_BRANCH\stats_status_container\stats_radx_button\ySIImage\filename" "interface\icons\Aid_Drug.dds"
	SetUIStringAlt "StatsMenu\GLOW_BRANCH\stats_status_container\stats_radaway_button\ySIImage\filename" "interface\icons\Aid_RadAway.dds"
	SetUIStringAlt "StatsMenu\GLOW_BRANCH\stats_status_container\stats_healing_mode\ySIImage\filename" "interface\icons\Apparel_Cloth_LabCoat.dds"
	SetUIStringAlt "InventoryMenu\GLOW_BRANCH\IM_MainRect\IM_ButtonRect\IM_EquipButton\ySIImage\filename" "interface\icons\Apparel_Other_PipBoy.dds"
	SetUIStringAlt "InventoryMenu\GLOW_BRANCH\IM_MainRect\IM_ButtonRect\IM_DropButton\ySIImage\filename" "interface\icons\Misc_Junk.dds"
	SetUIStringAlt "InventoryMenu\GLOW_BRANCH\IM_MainRect\IM_ButtonRect\IM_CancelButton\ySIImage\filename" "interface\icons\UI_Cancel.dds"
	SetUIStringAlt "InventoryMenu\GLOW_BRANCH\IM_MainRect\IM_ButtonRect\IM_HotkeyButton\ySIImage\filename" "interface\icons\Misc_Key.dds"
	SetUIStringAlt "InventoryMenu\GLOW_BRANCH\IM_MainRect\IM_ButtonRect\IM_RepairButton\ySIImage\filename" "interface\icons\Aid_WeaponRepairKit.dds"
	SetUIStringAlt "InventoryMenu\GLOW_BRANCH\IM_MainRect\IM_ButtonRect\IM_ModButton\ySIImage\filename" "interface\icons\Misc_WeaponMod.dds"
	SetUIStringAlt "InventoryMenu\GLOW_BRANCH\IM_MainRect\IM_ButtonRect\VUI+ModButtonVisibility\IM_ModButton\ySIImage\filename" "interface\icons\Misc_WeaponMod.dds"
	SetUIStringAlt "ItemModMenu\GLOW_BRANCH\IMM_MainRect\IMM_ButtonRect\IMM_ItemModButton\ySIImage\filename" "interface\icons\Misc_WeaponMod.dds"
	SetUIStringAlt "ItemModMenu\GLOW_BRANCH\IMM_MainRect\IMM_ButtonRect\IMM_CancelButton\ySIImage\filename" "interface\icons\UI_Accept.dds"
	SetUIStringAlt "RepairMenu\GLOW_BRANCH\RM_MainRect\RM_ButtonRect\RM_RepairButton\ySIImage\filename" "interface\icons\Aid_WeaponRepairKit.dds"
	SetUIStringAlt "RepairMenu\GLOW_BRANCH\RM_MainRect\RM_ButtonRect\RM_CancelButton\ySIImage\filename" "interface\icons\UI_Accept.dds"

	; Mod support
	SetUIStringAlt "MapMenu\GLOW_BRANCH\MM_MainRect\MM_ButtonRect\MM_ButtonVend\ySIImage\filename" "interface\icons\Apparel_Headgear_Headphones.dds"
	SetUIStringAlt "MapMenu\GLOW_BRANCH\MM_MainRect\MM_ButtonRect\MM_ButtonCasino\ySIImage\filename" "interface\icons\Misc_Card.dds"
	SetUIStringAlt "MapMenu\GLOW_BRANCH\MM_MainRect\MM_ButtonRect\MM_ButtonClear\ySIImage\filename" "interface\icons\Misc_QuestItem.dds"

	; Enable ySI icons (Resistance)
	SetUIStringAlt "StatsMenu\GLOW_BRANCH\stats_status_container\stats_radiation_container\VUI+Resistances\VUI+EnergyResistLabel\ySIImage\filename" "interface\icons\Ammo_Energy.dds"
	SetUIStringAlt "StatsMenu\GLOW_BRANCH\stats_status_container\stats_radiation_container\VUI+Resistances\VUI+FireResistLabel\ySIImage\filename" "interface\icons\Ammo_Flamer.dds"
	SetUIStringAlt "StatsMenu\GLOW_BRANCH\stats_status_container\stats_radiation_container\VUI+Resistances\VUI+PoisonResistLabel\ySIImage\filename" "interface\icons\Ammo_PoisonShot.dds"
	SetUIStringAlt "StatsMenu\GLOW_BRANCH\stats_status_container\stats_radiation_container\stats_rad_resist\stats_rad_resist_label\ySIImage\filename" "interface\icons\Aid_RadAway.dds"
	if eval Goo1.AuxVarGetFlt "*_DarNifiedUINV.esp" != 1 && Goo1.AuxVarGetFlt "*_Vanilla UI Extension" != 1
		ModUIFloat "StatsMenu\GLOW_BRANCH\stats_status_container\stats_radiation_container\stats_rad_resist\stats_rad_resist_label\ySIImage\y" 4
	endif

endif

; Weapon mod comparison
SetUIFloatAlt "ItemModMenu\GLOW_BRANCH\IMM_MainRect\IMM_CompareRect\visible" 0
SetUIFloatAlt "ItemModMenu\GLOW_BRANCH\IMM_MainRect\IMM_ItemModList\IMM_LeftBracket\visible" 0
SetUIFloatAlt "ItemModMenu\GLOW_BRANCH\IMM_MainRect\IMM_ItemModList\IMM_RightBracket\visible" 0
SetUIFloatAlt "ItemModMenu\GLOW_BRANCH\IMM_MainRect\IMM_ItemModList\lb_highlight_box\width" (GetUIFloatAlt "InventoryMenu\GLOW_BRANCH\IM_MainRect\IM_InventoryList\lb_highlight_box\width")
SetUIFloatAlt "ItemModMenu\GLOW_BRANCH\IMM_MainRect\IMM_ModCompare\DAMInfo\visible" 1
SetUIFloatAlt "ItemModMenu\GLOW_BRANCH\IMM_MainRect\IMM_ModCompare\ConditionInfo\visible" 1
SetUIFloatAlt "ItemModMenu\GLOW_BRANCH\IMM_MainRect\IMM_ModCompare\AmmoInfo\visible" 1
SetUIFloatAlt "ItemModMenu\GLOW_BRANCH\IMM_MainRect\IMM_ModCompare\ValueInfo\visible" 1
SetUIFloatAlt "ItemModMenu\GLOW_BRANCH\IMM_MainRect\IMM_ModCompare\WeightInfo\visible" 1
SetUIFloatAlt "ItemModMenu\GLOW_BRANCH\IMM_MainRect\IMM_ModCompare\StrengthReqInfo\visible" 1
SetUIFloatAlt "ItemModMenu\GLOW_BRANCH\IMM_MainRect\IMM_ModCompare\EffectsInfo\visible" 1
SetUIFloatAlt "ItemModMenu\GLOW_BRANCH\IMM_MainRect\IMM_ModCompare\ModInfoOne\CI_TopLine\visible" 0
SetUIFloatAlt "ItemModMenu\GLOW_BRANCH\IMM_MainRect\IMM_ModCompare\ModInfoOne\CI_ValueText\systemcolor" 3
SetUIFloatAlt "ItemModMenu\GLOW_BRANCH\IMM_MainRect\IMM_ItemIcon\height" 380
SetUIFloatAlt "ItemModMenu\GLOW_BRANCH\IMM_MainRect\IMM_ItemIcon\width" 380
SetUIFloatAlt "ItemModMenu\GLOW_BRANCH\IMM_MainRect\IMM_ModCompare\x" (GetUIFloatAlt "InventoryMenu\GLOW_BRANCH\IM_MainRect\IM_ItemInfoRect\x")
SetUIFloatAlt "ItemModMenu\GLOW_BRANCH\IMM_MainRect\IMM_ModCompare\y" (GetUIFloatAlt "InventoryMenu\GLOW_BRANCH\IM_MainRect\IM_ItemInfoRect\y")
SetUIFloatAlt "ItemModMenu\GLOW_BRANCH\IMM_MainRect\IMM_ModCompare\width" (GetUIFloatAlt "InventoryMenu\GLOW_BRANCH\IM_MainRect\IM_ItemInfoRect\width")
SetUIFloatAlt "ItemModMenu\GLOW_BRANCH\IMM_MainRect\IMM_ModCompare\height" (GetUIFloatAlt "InventoryMenu\GLOW_BRANCH\IM_MainRect\IM_ItemInfoRect\height")
SetUIFloatAlt "ItemModMenu\GLOW_BRANCH\IMM_MainRect\IMM_ModCompare\ModInfoOne\y" (GetUIFloatAlt "ItemModMenu\GLOW_BRANCH\IMM_MainRect\IMM_ModCompare\AmmoInfo\y")
SetUIFloatAlt "ItemModMenu\GLOW_BRANCH\IMM_MainRect\IMM_ModCompare\EffectsInfo\y" ((GetUIFloatAlt "ItemModMenu\GLOW_BRANCH\IMM_MainRect\IMM_ModCompare\EffectsInfo\y") + ((GetUIFloatAlt "ItemModMenu\GLOW_BRANCH\IMM_MainRect\IMM_ModCompare\ConditionInfo\y") - (GetUIFloatAlt "ItemModMenu\GLOW_BRANCH\IMM_MainRect\IMM_ModCompare\ValueInfo\y")))
if eval Goo1.AuxVarGetFlt "*_DarNifiedUINV.esp"
	SetUIFloatAlt "ItemModMenu\GLOW_BRANCH\IMM_MainRect\IMM_ModCompare\ConditionInfo\CNDArrows\x" 119.5
	SetUIFloatAlt "ItemModMenu\GLOW_BRANCH\IMM_MainRect\IMM_ModCompare\ConditionInfo\CNDArrows\y" 12.5
	SetUIFloatAlt "ItemModMenu\GLOW_BRANCH\IMM_MainRect\IMM_ModCompare\ConditionInfo\CNDArrows\height" 15
elseif eval Goo1.AuxVarGetFlt "*_Vanilla UI Plus.esp"
	SetUIFloatAlt "ItemModMenu\GLOW_BRANCH\IMM_MainRect\IMM_ItemIcon\x" 480.55
	SetUIFloatAlt "ItemModMenu\GLOW_BRANCH\IMM_MainRect\IMM_ItemIcon\y" 15
else
	SetUIFloatAlt "ItemModMenu\GLOW_BRANCH\IMM_MainRect\IMM_ModCompare\ConditionInfo\CNDArrows\x" 119.5
	SetUIFloatAlt "ItemModMenu\GLOW_BRANCH\IMM_MainRect\IMM_ModCompare\ConditionInfo\CNDArrows\y" 15
	SetUIFloatAlt "ItemModMenu\GLOW_BRANCH\IMM_MainRect\IMM_ModCompare\ConditionInfo\CNDArrows\height" 20
endif
if eval Goo1.AuxVarGetFlt "*_Vanilla UI Plus.esp" != 1
	SetUIFloatAlt "ItemModMenu\GLOW_BRANCH\IMM_MainRect\IMM_ModCompare\DAMInfo\alpha" 255
	SetUIFloatAlt "ItemModMenu\GLOW_BRANCH\IMM_MainRect\IMM_ModCompare\StrengthReqInfo\alpha" 255
	SetUIFloatAlt "ItemModMenu\GLOW_BRANCH\IMM_MainRect\IMM_ItemIcon\x" 450
	SetUIFloatAlt "ItemModMenu\GLOW_BRANCH\IMM_MainRect\IMM_ItemIcon\y" 10
	SetUIFloatAlt "ItemModMenu\GLOW_BRANCH\IMM_MainRect\IMM_HeadlineRect\IMM_ItemName\*:3\justify" 2
	SetUIFloatAlt "ItemModMenu\GLOW_BRANCH\IMM_MainRect\IMM_HeadlineRect\IMM_ItemName\*:3\x" ((GetUIFloatAlt "ItemModMenu\GLOW_BRANCH\IMM_MainRect\width") / 3.5)
endif

; Update weapon mod comparison on ItemModMenu open
SetOnMenuOpenEventHandler (CompileScript "PipBoyUITweaks\OpenItemModMenu.gek") 1 1061

; Update weapon mod comparison on weapon mod attachment
SetOnMenuClickEventHandler (begin function {int iMenuID, int iTileID, string_var sTileName}
	CallAfterFrames 0 (begin function {int iModMask}
		if eval iModMask != (GetMenuTargetRef 1061).GetWeaponRefModFlags
			Call (CompileScript "PipBoyUITweaks\GetModStats.gek")
			Call (CompileScript "PipBoyUITweaks\OnMouse1061.gek") 1061
		endif
	end) 1 (GetMenuTargetRef 1061).GetWeaponRefModFlags
end) 1 "ItemModMenu\GLOW_BRANCH\IMM_MainRect\IMM_ItemModList\IMM_ItemModListTemplateRect"

; Update numeric repair
SetOnMenuOpenEventHandler (CompileScript "PipBoyUITweaks\MouseOverRepair.gek") 1 1035
SetOnMouseoverChangeEventHandler (CompileScript "PipBoyUITweaks\MouseOverRepair.gek") 1 1035
SetOnKBCTrlUIDeltaHandler 1 (CompileScript "PipBoyUITweaks\MouseOverRepair.gek") 0 1035
SetOnMenuClickEventHandler ({int iMenuID, int iTileID, string_var sTileName} => CallAfterFrames 0 (CompileScript "PipBoyUITweaks\MouseOverRepair.gek") 1 1035) 1 "RepairMenu#14"
SetOnMouseoverChangeEventHandler (CompileScript "PipBoyUITweaks\MouseOverRepairServices.gek") 1 1058
SetOnKBCTrlUIDeltaHandler 1 (CompileScript "PipBoyUITweaks\MouseOverRepairServices.gek") 0 1058
SetOnMenuClickEventHandler ({int iMenuID, int iTileID, string_var sTileName} => CallAfterFrames 0 (CompileScript "PipBoyUITweaks\MouseOverRepairServices.gek") 1 1058) 1 "RepairServicesMenu#17"

; Enable colorful stats info (Crippled limbs)
SetOnMenuOpenEventHandler (CompileScript "PipBoyUITweaks\OpenMapMenu.gek") 1 1023

; Enable colorful stats info (RADS)
InjectUIXML "StatsMenu\GLOW_BRANCH\stats_status_container\stats_RAD_button\stb_text_box" "data\menus\prefabs\Stentorious\PipBoyUITweaks\StatsMenu\stats_RAD_button.xml"
SetUIFloatAlt "StatsMenu\GLOW_BRANCH\stats_status_container\stats_RAD_button\stb_text_box\button_text\visible" 0

; Enable colorful stats info (H2O)
InjectUIXML "StatsMenu\GLOW_BRANCH\stats_status_container\stats_H20_button\stb_text_box" "data\menus\prefabs\Stentorious\PipBoyUITweaks\StatsMenu\stats_H20_button.xml"
SetUIFloatAlt "StatsMenu\GLOW_BRANCH\stats_status_container\stats_H20_button\stb_text_box\button_text\visible" 0

; Enable colorful stats info (FOD)
InjectUIXML "StatsMenu\GLOW_BRANCH\stats_status_container\stats_FOD_button\stb_text_box" "data\menus\prefabs\Stentorious\PipBoyUITweaks\StatsMenu\stats_FOD_button.xml"
SetUIFloatAlt "StatsMenu\GLOW_BRANCH\stats_status_container\stats_FOD_button\stb_text_box\button_text\visible" 0

; Enable colorful stats info (SLP)
InjectUIXML "StatsMenu\GLOW_BRANCH\stats_status_container\stats_SLP_button\stb_text_box" "data\menus\prefabs\Stentorious\PipBoyUITweaks\StatsMenu\stats_SLP_button.xml"
SetUIFloatAlt "StatsMenu\GLOW_BRANCH\stats_status_container\stats_SLP_button\stb_text_box\button_text\visible" 0

; Enable critical chance ItemInfo component
SetUIFloatAlt "InventoryMenu\GLOW_BRANCH\IM_MainRect\IM_ItemInfoRect\CritInfo\CI_ValueText\wrapwidth" 150
SetUIFloatAlt "ItemModMenu\GLOW_BRANCH\IMM_MainRect\IMM_ModCompare\CritInfo\CI_ValueText\wrapwidth" 150

; Enable colorful stats info (Encumbrance)
SetUIFloatAlt "InventoryMenu\GLOW_BRANCH\IM_MainRect\IM_HeadlineRect\IM_Headline_PlayerWGInfo\CI_ValueText\visible" 0

; Enable colorful stats info (Hit points)
SetUIFloatAlt "InventoryMenu\GLOW_BRANCH\IM_MainRect\IM_HeadlineRect\IM_Headline_PlayerHPInfo\CI_ValueText\visible" 0
SetUIFloatAlt "StatsMenu\GLOW_BRANCH\stats_hp_info\CI_ValueText\visible" 0

; Enable colorful stats info (Action points)
SetUIFloatAlt "StatsMenu\GLOW_BRANCH\stats_ap_info\CI_ValueText\visible" 0

; Enable separate DT/DR
SetEventHandler "OnActorUnequip" (begin Function {ref rEquipper, ref rEquipped}
	if eval GetActiveMenuMode == 1002 && GetType rEquipped == 24
		CallAfterFrames 0 (CompileScript "PipBoyUITweaks\UpdateDTDR.gek") 1
	endif
end) "first"::Player

; Expanded menu support
SetOnMenuOpenEventHandler ({int iMenuID} => CallAfterFrames 0 (CompileScript "PipBoyUITweaks\OpenContainerMenu.gek") 1 iMenuID) 1 1008
SetOnMenuOpenEventHandler (CompileScript "PipBoyUITweaks\OpenBarterMenu.gek") 1 1053
SetOnMenuOpenEventHandler (CompileScript "PipBoyUITweaks\OpenRepairServicesMenu.gek") 1 1058
SetOnMenuOpenEventHandler ({int iMenuID} => CallAfterFrames 0 (CompileScript "PipBoyUITweaks\OpenRecipeMenu.gek") 1 iMenuID) 1 1077
SetOnMouseoverChangeEventHandler (CompileScript "PipBoyUITweaks\MouseOver.gek") 1 1008
SetOnKBCTrlUIDeltaHandler 1 (CompileScript "PipBoyUITweaks\MouseOver.gek") 0 1008
SetOnMouseoverChangeEventHandler (CompileScript "PipBoyUITweaks\MouseOver.gek") 1 1053
SetOnKBCTrlUIDeltaHandler 1 (CompileScript "PipBoyUITweaks\MouseOver.gek") 0 1053
SetOnMouseoverChangeEventHandler (CompileScript "PipBoyUITweaks\MouseOverRecipe.gek") 1 1077
SetOnKBCTrlUIDeltaHandler 1 (CompileScript "PipBoyUITweaks\MouseOverRecipe.gek") 0 1077
SetOnMenuClickEventHandler (CompileScript "PipBoyUITweaks\MenuClick.gek") 1 "ContainerMenu#20"
SetOnMenuClickEventHandler (CompileScript "PipBoyUITweaks\MenuClick.gek") 1 "BarterMenu#23"
SetGameMainLoopCallback (CompileScript "PipBoyUITweaks\MenuLoop.gek") 1 5 2

; Clear INI cache
CloseFileSO "Stentorious\PipBoyUITweaks.ini" 0

; Menu update event handlers
SetOnMenuOpenEventHandler (CompileScript "PipBoyUITweaks\OpenInventoryMenu.gek") 1 1002
SetOnMenuOpenEventHandler (CompileScript "PipBoyUITweaks\OpenStatsMenu.gek") 1 1003
SetOnMouseoverChangeEventHandler (CompileScript "PipBoyUITweaks\MouseOver.gek") 1 1002
SetOnKBCTrlUIDeltaHandler 1 (CompileScript "PipBoyUITweaks\MouseOver.gek") 0 1002
SetOnMouseoverChangeEventHandler (CompileScript "PipBoyUITweaks\OnMouse1061.gek") 1 1061
SetOnKBCTrlUIDeltaHandler 1 (CompileScript "PipBoyUITweaks\OnMouse1061.gek") 0 1061
SetOnMenuClickEventHandler (CompileScript "PipBoyUITweaks\MenuClick.gek") 1 "InventoryMenu#29"

; Inventory update event handlers
SetEventHandler "OnDrop" (CompileScript "PipBoyUITweaks\OnDrop.gek") "first"::Player
SetEventHandler "OnActorEquip" (CompileScript "PipBoyUITweaks\OnActorEquip.gek") "first"::Player

; Actor Value update handlers
if eval GetUIFloatAlt "HUDMainMenu\_PlrStat+ActionPointPer" == -1
	float fAVPercent = 1.0
	if eval (float fBaseValue = Player.GetPermanentActorValue ActionPoints) > 0
		fAVPercent = (Player.GetAV ActionPoints) / fBaseValue
	endif
	SetUIFloatAlt "HUDMainMenu\_PlrStat+ActionPointPer" fAVPercent
	SetOnActorValueChangeEventHandler 1 (begin Function {int iAVCode, float fPrevValue, float fNewValue}
		if eval (fPrevValue = Player.GetPermanentActorValue ActionPoints) > 0
			fNewValue = (Player.GetAV ActionPoints) / fPrevValue
		else
			fNewValue = 1.0
		endif
		SetUIFloatAlt "HUDMainMenu\_PlrStat+ActionPointPer" fNewValue
	end) 0 12	; ActionPoints
endif
if eval GetUIFloatAlt "HUDMainMenu\_PlrStat+HealthPer" == -1
	float fAVPercent = 1.0
	if eval (float fBaseValue = Player.GetPermanentActorValue Health) > 0
		fAVPercent = (Player.GetAV Health) / fBaseValue
	endif
	SetUIFloatAlt "HUDMainMenu\_PlrStat+HealthPer" fAVPercent
	SetOnActorValueChangeEventHandler 1 (begin Function {int iAVCode, float fPrevValue, float fNewValue}
		if eval (fPrevValue = Player.GetPermanentActorValue Health) > 0
			fNewValue = (Player.GetAV Health) / fPrevValue
		else
			fNewValue = 1.0
		endif
		SetUIFloatAlt "HUDMainMenu\_PlrStat+HealthPer" fNewValue
	end) 0 16
endif
if eval GetUIFloatAlt "HUDMainMenu\_PlrStat+RAD" == -1
	SetUIFloatAlt "HUDMainMenu\_PlrStat+RAD" (Player.GetAV RadiationRads)
	SetOnActorValueChangeEventHandler 1 ({int iAVCode, float fPrevValue, float fNewValue} => SetUIFloatAlt "HUDMainMenu\_PlrStat+RAD" (Player.GetAV RadiationRads)) 0 54		; RadiationRads
endif
if eval GetUIFloatAlt "HUDMainMenu\_PlrStat+H2O" == -1
	SetUIFloatAlt "HUDMainMenu\_PlrStat+H2O" (Player.GetAV Dehydration)
	SetOnActorValueChangeEventHandler 1 ({int iAVCode, float fPrevValue, float fNewValue} => SetUIFloatAlt "HUDMainMenu\_PlrStat+H2O" (Player.GetAV Dehydration)) 0 73			; Dehydration
endif
if eval GetUIFloatAlt "HUDMainMenu\_PlrStat+FOD" == -1
	SetUIFloatAlt "HUDMainMenu\_PlrStat+FOD" (Player.GetAV Hunger)
	SetOnActorValueChangeEventHandler 1 ({int iAVCode, float fPrevValue, float fNewValue} => SetUIFloatAlt "HUDMainMenu\_PlrStat+FOD" (Player.GetAV Hunger)) 0 74				; Hunger
endif
if eval GetUIFloatAlt "HUDMainMenu\_PlrStat+SLP" == -1
	SetUIFloatAlt "HUDMainMenu\_PlrStat+SLP" (Player.GetAV SleepDeprevation)
	SetOnActorValueChangeEventHandler 1 ({int iAVCode, float fPrevValue, float fNewValue} => SetUIFloatAlt "HUDMainMenu\_PlrStat+SLP" (Player.GetAV SleepDeprevation)) 0 75		; SleepDeprevation
endif

; Setting update handlers
if eval GetUIFloatAlt "HUDMainMenu\_Setting+Hardcore" == -1
	SetOnSettingsUpdateEventHandler 1 ({} => SetUIFloatAlt "HUDMainMenu\_Setting+Hardcore" (Player.IsHardcore)) 0
endif
SetUIFloatAlt "HUDMainMenu\_Setting+Hardcore" (Player.IsHardcore)
