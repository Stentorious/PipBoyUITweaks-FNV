int	iMenuID

int iType
int iComp
int iModSlot
int iModMask
int iModType
int iSlotMask
int iSystemColor
int iSystemColorTemp
float fEquipWg
float fEquipVal
float fEquipStr
float fItemWg
float fItemVal
float fItemStr
float fItemRoF
float fItemCrit
float fItemStat
float fItemPrj
float iColor
ref rItem
array_var aTemp
string_var sTilePath
string_var sUIPath
string_var sTemp

begin Function {iMenuID}

	; Check if selected inventory weapon/armor should have stat colors and/or extended stat comparisons
	if eval IsReference (rItem = GetSelectedItemRef) == 0
		return
	endif

	iType = GetActiveUIComponentID
	if eval iMenuID == 1002 && iType == 29
		sTilePath = "InventoryMenu\GLOW_BRANCH\IM_MainRect\IM_ItemInfoRect\"
		iSystemColorTemp = iSystemColor = 4
	elseif eval iMenuID == 1008 && iType == 20
		sTilePath = "ContainerMenu\NOGLOW_BRANCH\CM_ItemData\"
		iSystemColorTemp = iSystemColor = 1
	elseif eval iMenuID == 1053 && iType == 23
		sTilePath = "BarterMenu\NOGLOW_BRANCH\BM_ItemData\"
		iSystemColorTemp = iSystemColor = 1
	else
		return
	endif

	Goo1.AuxVarSetRef "*PipTweaks_InvRef" (rItem.GBO)

	fItemVal = rItem.GetCalculatedItemValue
	fItemWg = #(GetUIString (sTilePath + "WeightInfo\CI_ValueText\string"))

	if eval Goo1.AuxVarGetFlt "*_Tweaks:bIndividialItemStats" == 0 && (iComp = rItem.GetRefCount) > 1
		fItemWg /= iComp
	endif

	; Value/Weight ratio
	if eval fItemWg <= 0 || fItemVal <= 0
		sTemp = "--"
	else
		fItemStat = fItemVal / fItemWg
		if fItemStat < 1000
			sTemp = sv_Construct "%.1f" fItemStat
		else
			sTemp = "1000+"
		endif
	endif
	SetUIStringAlt (sTilePath + "ValWGInfo\_Value") (sTemp)

	; Reset tiles
	SetUIFloatAlt (sTilePath + "AddictionInfo\visible") 0

	; Extended menu support
	if eval GetUIFloatAlt "HUDMainMenu\_PipTweaks+ExtMenuSupport" != 1 && iMenuID != 1002
		return
	endif

	if eval (iType = rItem.GetType) == 40 || iType == 24

		Goo1.AuxVarSetFlt "*PipTweaks_FLAG" 1
		iColor = Goo1.AuxVarGetFlt "*PipTweaks_INI" 3

		if eval iType == 40

			; Display skill requirement
			aTemp = Ar_Map 33::"big_guns", 34::"energy", 35::"explosives", 38::"melee", 41::"sm_arms", 45::"unarmed"
			iComp = rItem.GetReqSkill
			fItemStat = rItem.GetWeaponSkill
			if Player.GetAV (fItemStat) < iComp
				SetUIFloatAlt (sTilePath + "SkillInfo\CI_ValueText\brightness") 128
			else
				SetUIFloatAlt (sTilePath + "SkillInfo\CI_ValueText\brightness") 255
			endif
			SetUIStringAlt (sTilePath + "SkillInfo\Icon\filename") "Interface\Icons\TypeIcons\weap_skill_icon_%z.dds" (aTemp[fItemStat])
			SetUIStringAlt (sTilePath + "SkillInfo\_value") "%g" iComp
			aTemp = Ar_Null

			; Calculate item mod effects
			iModMask = rItem.GetWeaponRefModFlags
			fItemRoF = rItem.GetFireRate
			iModSlot = 0
			while 4 > (iModSlot += 1)
				if eval GetBit iModMask (iModSlot - 1)
					iModType = rItem.GetWeaponItemModEffect iModSlot
					if eval iModType == 8
						fItemRoF *= (1 + (rItem.GetWeaponItemModValue1 iModSlot))
					elseif eval iModType == 12
						fItemPrj += rItem.GetWeaponItemModValue1 iModSlot
					endif
				endif
			loop

			; Display critical hit damage
			fItemPrj += rItem.GetNumProj
			fItemStat = rItem.GetCalculatedWeaponDamage
			if fItemStat > 0
				fItemStat += Player.GetPerkModifier 2 (rItem.GetCritDam * Goo1.AuxVarGetFlt "*_Tweaks:bScaleCriticalDamage" 1) rItem Player
				if fItemPrj > 1
					sTemp = sv_Construct "%.1fx%g" (fItemStat / fItemPrj) fItemPrj
				else
					sTemp = sv_Construct "%.0f" fItemStat
				endif
			else
				sTemp = "--"
			endif
			SetUIStringAlt (sTilePath + "CritDAMInfo\_value") (sTemp)

			; Update weapon critical chance
			if eval GetUIFloatAlt "HUDMainMenu\_PipTweaks+CritInfo" == 1
				fItemCrit = Flr (Player.GetAV CritChance)
				if eval rItem.GetIsAutomatic && Goo1.AuxVarGetFlt "*_Tweaks:bCritChanceIgnoresFireRate" == 0
					fItemCrit /= fItemRoF
				endif
				fItemCrit *= rItem.GetCritPerc
				SetUIStringAlt (sTilePath + "CritInfo\_value") "%.2f%%" (GetMaxOf 0 (GetMinOf 100 (Player.GetPerkModifier 1 fItemCrit rItem Goo1)))
			endif

		endif

		; Extended stat comparison
		if eval Goo1.AuxVarGetFlt "*PipTweaks_INI" 0

			; Get weapon stats
			if iType == 40

				if eval Player.GetEquippedWeaponRef
					iComp = 1
					fEquipStr = Goo1.AuxVarGetFlt "*PipTweaks_Stat" 0
					fEquipWg = Goo1.AuxVarGetFlt "*PipTweaks_Stat" 1
					fEquipVal = Goo1.AuxVarGetFlt "*PipTweaks_Stat" 2
					fItemStr = #(GetUIString (sTilePath + "StrengthReqInfo\_value"))

					; Update STR
					if eval fItemStr == fEquipStr
						sTemp = ""
						iSystemColorTemp = iSystemColor
					elseif eval fItemStr > fEquipStr
						sTemp = "+"
						if eval iColor
							iSystemColorTemp = 3
						endif
					else
						sTemp = "-"
						if eval iColor
							iSystemColorTemp = 2
						endif
					endif
					SetUIStringAlt (sTilePath + "StrengthReqInfo\_value") "%g%z" fItemStr sTemp
					SetUIFloatAlt (sTilePath + "StrengthReqInfo\CI_ValueText\systemcolor") iSystemColorTemp

				else
					iComp = 0
				endif

			; Get armor stats
			else
				iSlotMask = rItem.GetESM
				If eval GetBit iSlotMask 2
					iComp = 1
					fEquipWg = Goo1.AuxVarGetFlt "*PipTweaks_Stat" 3
					fEquipVal = Goo1.AuxVarGetFlt "*PipTweaks_Stat" 4
				elseif iSlotMask == 18432
					iComp = 1
					fEquipWg = Goo1.AuxVarGetFlt "*PipTweaks_Stat" 5
					fEquipVal = Goo1.AuxVarGetFlt "*PipTweaks_Stat" 6
				elseif rItem.GetEquipType == 8
					iComp = 1
					fEquipWg = Goo1.AuxVarGetFlt "*PipTweaks_Stat" 7
					fEquipVal = Goo1.AuxVarGetFlt "*PipTweaks_Stat" 8
				endif
			endif

			; Update WG/VAl on valid stat comparison
			if eval iComp

				; Update WG
				if eval fEquipWg == fItemWg
					sTemp = ""
					iSystemColorTemp = iSystemColor
				elseif eval fEquipWg > fItemWg
					if fItemWg > 0
						sTemp = "+"
					endif
					if eval iColor
						iSystemColorTemp = 3
					endif
				else
					if fItemWg > 0
						sTemp = "-"
					endif
					if eval iColor
						iSystemColorTemp = 2
					endif
				endif
				SetUIStringAlt (sTilePath + "WeightInfo\_value") "%.2f%z" (#(GetUIString (sTilePath + "WeightInfo\CI_ValueText\string"))) sTemp
				SetUIFloatAlt (sTilePath + "WeightInfo\CI_ValueText\systemcolor") iSystemColorTemp

				; Update Val
				if eval fItemVal == fEquipVal
					sTemp = ""
					iSystemColorTemp = iSystemColor
				elseif eval fItemVal > fEquipVal
					if fItemVal > 0
						sTemp = "+"
					endif
					if eval iColor
						iSystemColorTemp = 3
					endif
				else
					if fItemVal > 0
						sTemp = "-"
					endif
					if eval iColor
						iSystemColorTemp = 2
					endif
				endif

				SetUIStringAlt (sTilePath + "ValueInfo\_value") "%g%z" (#(GetUIString (sTilePath + "ValueInfo\CI_ValueText\string"))) sTemp
				SetUIFloatAlt (sTilePath + "ValueInfo\CI_ValueText\systemcolor") iSystemColorTemp

			; Reset color if selecting weapon/armor item without valid stat comparison
			else
				SetUIFloatAlt (sTilePath + "WeightInfo\CI_ValueText\systemcolor") iSystemColor
				SetUIFloatAlt (sTilePath + "ValueInfo\CI_ValueText\systemcolor") iSystemColor
				SetUIFloatAlt (sTilePath + "StrengthReqInfo\CI_ValueText\systemcolor") iSystemColor
			endif
		endif

		; Update DAM/DPS/DR/DT color
		if iType == 40
			iSystemColorTemp = iSystemColor
			if eval iColor
				sUIPath = GetUIString (sTilePath + "DAMInfo\_Value")
				if eval sUIPath[-1] == "+"
					iSystemColorTemp = 3
				elseif eval sUIPath[-1] == "-"
					iSystemColorTemp = 2
				endif
			endif
			SetUIFloatAlt (sTilePath + "DAMInfo\CI_ValueText\systemcolor") iSystemColorTemp
			iSystemColorTemp = iSystemColor
			if eval iColor
				sUIPath = GetUIString (sTilePath + "DPSInfo\_Value")
				if eval sUIPath[-1] == "+"
					iSystemColorTemp = 3
				elseif eval sUIPath[-1] == "-"
					iSystemColorTemp = 2
				endif
			endif
			SetUIFloatAlt (sTilePath + "DPSInfo\CI_ValueText\systemcolor") iSystemColorTemp
		else
			iSystemColorTemp = iSystemColor
			if eval iColor
				sUIPath = GetUIString (sTilePath + "DamageResistInfo\_Value")
				if eval sUIPath[-1] == "+"
					iSystemColorTemp = 3
				elseif eval sUIPath[-1] == "-"
					iSystemColorTemp = 2
				endif
			endif
			SetUIFloatAlt (sTilePath + "DamageResistInfo\CI_ValueText\systemcolor") iSystemColorTemp
			iSystemColorTemp = iSystemColor
			if eval iColor
				sUIPath = GetUIString (sTilePath + "DamageThresholdInfo\_Value")
				if eval sUIPath[-1] == "+"
					iSystemColorTemp = 3
				elseif eval sUIPath[-1] == "-"
					iSystemColorTemp = 2
				endif
			endif
			SetUIFloatAlt (sTilePath + "DamageThresholdInfo\CI_ValueText\systemcolor") iSystemColorTemp
		endif

		; Set AmmoInfo icon
		if eval GetUIFloatAlt "HUDMainMenu\_PipTweaks+ySI_AmmoInfo" == 1

			; Set armor class icon if armor
			if eval (iType = GetArmorClass (rItem.GBO))
				sUIPath = "interface\icons\Apparel_Armor"
				if eval rItem.IsPA
					sUIPath += "D_Power.dds"
					SetUIStringAlt (sTilePath + "AmmoInfo\_title") (GetStringSetting "sKBPower")
				elseif iType == 1
					sUIPath += "A_Light.dds"
				elseif iType == 2
					sUIPath += "B_Medium.dds"
				else
					sUIPath += "C_Heavy.dds"
				endif
				SetUIStringAlt (sTilePath + "AmmoInfo\ySIImage\filename") (sUIPath)

			; Set ammo icon if weapon uses ammo
			elseif eval IsFormValid (rItem = rItem.GetWeaponAmmo)
				if GetType rItem == 85
					rItem = ListGetNthForm rItem 0
				endif
				RunScriptSnippet 0 "SetUIStringAlt %q%zAmmoInfo\ySIImage\filename%q (ySIGetTrait %qicon%q %q%i%q)" sTilePath rItem

			; Set item icon as fallback
			else
				SetUIStringAlt (sTilePath + "AmmoInfo\ySIImage\filename") (GetUIString (GetActiveUIComponentFullName + "/ySIImage/filename"))
			endif
		endif

	; Reset color if selecting non weapon/armor item
	else

		if eval iType == 47

			; Addiction chance
			if eval Goo1.AuxVarGetFlt "*PipTweaks_INI" 1

				rItem = rItem.GBO
				if eval IsFormValid (GetAddictionEffect rItem)

					sTemp = "--"
					iType = 0
					iComp = GetNumEffects rItem
					while (iComp -= 1) > -1
						if eval GetNthEffectBase rItem iComp == UMON
							iType = 1
							break
						endif
					loop

					if eval iType
						fItemStat = (GetNthEffectTraitNumeric rItem iComp 0)
						if eval GetIngestibleFlag rItem 4
							fItemStat *= ((GetGS "fMagicMedicineSkillBase") + ((GetGS "fMagicMedicineSkillMult") * ((Player.GetAV Medicine) / 100)))
						elseif eval GetIngestibleFlag rItem 2
							fItemStat *= ((GetGS "fMagicSurvivalSkillBase") + ((GetGS "fMagicSurvivalSkillMult") * ((Player.GetAV Survival) / 100)))
						endif
						sTemp = sv_Construct "%g%%" (GetMinOf 100 (Flr (((Player.GetPerkModifier 23 (GetMaxOf 0 ((((Player.GetSpellUsageNumEx rItem) + fItemStat) - (GetGS "fAddictionUsageMonitorThreshold")) / 100.0))) * 100) + 0.5)))
					else
						if eval (fItemStat = (Player.GetPerkModifier 23 (GetAddictionChance rItem))) > 0
							sTemp = sv_Construct "%g%%" (GetMinOf 100 (Flr ((fItemStat * 100) + 0.5)))
						endif
					endif

					if eval (sTemp) != "--"
						if eval Player.IsSpellTarget (GetAddictionEffect rItem)
							sTemp = "--"
						endif
						SetUIStringAlt (sTilePath + "AddictionInfo\_value") "%z" sTemp
						SetUIFloatAlt (sTilePath + "AddictionInfo\visible") 1
					endif

				endif
			endif

		endif

		if eval Goo1.AuxVarGetFlt "*PipTweaks_FLAG"
			Goo1.AuxVarSetFlt "*PipTweaks_FLAG" 0
			SetUIFloatAlt (sTilePath + "WeightInfo\CI_ValueText\systemcolor") iSystemColor
			SetUIFloatAlt (sTilePath + "ValueInfo\CI_ValueText\systemcolor") iSystemColor
			SetUIFloatAlt (sTilePath + "StrengthReqInfo\CI_ValueText\systemcolor") iSystemColor
		endif

	endif

	sv_Destruct sTilePath sUIPath sTemp

end
