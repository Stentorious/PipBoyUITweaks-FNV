int iSize
int iSlotMask
int iType
float fWeapWg
float fWeapVal
float fWeapStr
float fBodyWg
float fBodyVal
float fEyesWg
float fEyesVal
float fHeadWg
float fHeadVal
ref rItem
array_var aEquippedItems

begin Function {}

	; Cache equipped item stats
	aEquippedItems = Player.GetEquippedItemRefs
	if eval (iSize = Ar_Size aEquippedItems) > 0
		while (iSize -= 1) > -1
			if eval IsFormValid (rItem = aEquippedItems[iSize])
				iSlotMask = rItem.GetESM
				iType = rItem.GetEquipType

				; Weapon
				if eval rItem.GetType == 40
					fWeapWg = Player.GetCalculatedEquippedWeight 0 32
					fWeapVal = rItem.GetCalculatedItemValue
					fWeapStr = GetMaxOf 0 ((rItem.GetReqStr) - Player.GetPerkModifier 53 0 rItem)

				; Armor (Body)
				elseif eval GetBit iSlotMask 2
					fBodyWg = rItem.GetWeight
					fBodyVal = rItem.GetCalculatedItemValue

				; Armor (Eyes)
				elseif iSlotMask == 18432
					fEyesWg = rItem.GetWeight
					fEyesVal = rItem.GetCalculatedItemValue

				; Armor (Head)
				elseif iType == 8 && GetBit iSlotMask 11 != 1
					fHeadWg = rItem.GetWeight
					fHeadVal = rItem.GetCalculatedItemValue

				; Armor (Eyes + Head)
				elseif iType == 8
					fHeadWg = fEyesWg = rItem.GetWeight
					fHeadVal = fEyesVal = rItem.GetCalculatedItemValue
				endif
			endif
		loop
	endif

	; "Modify Light Items" perk modifier
	if eval Player.GetPerkModifier 55 0
		if eval fBodyWg <= 2
			fBodyWg /= 2
		endif
		if eval fEyesWg <= 2
			fEyesWg /= 2
		endif
		if eval fHeadWg <= 2
			fHeadWg /= 2
		endif
	endif

	; "bWeightlessItems" lStewieAl Tweak
	if eval Goo1.AuxVarGetFlt "*_Tweaks:bWeightlessItems" 0
		fWeapWg *= Goo1.AuxVarGetFlt "*_Tweaks:bWeightlessItems" 5
		fBodyWg *= Goo1.AuxVarGetFlt "*_Tweaks:bWeightlessItems" 3
		fEyesWg *= Goo1.AuxVarGetFlt "*_Tweaks:bWeightlessItems" 3
		fHeadWg *= Goo1.AuxVarGetFlt "*_Tweaks:bWeightlessItems" 3
	endif

	Goo1.AuxVarSetFlt "*PUI_Stat" fWeapStr 0
	Goo1.AuxVarSetFlt "*PUI_Stat" fWeapWg 1
	Goo1.AuxVarSetFlt "*PUI_Stat" fWeapVal 2
	Goo1.AuxVarSetFlt "*PUI_Stat" fBodyWg 3
	Goo1.AuxVarSetFlt "*PUI_Stat" fBodyVal 4
	Goo1.AuxVarSetFlt "*PUI_Stat" fEyesWg 5
	Goo1.AuxVarSetFlt "*PUI_Stat" fEyesVal 6
	Goo1.AuxVarSetFlt "*PUI_Stat" fHeadWg 7
	Goo1.AuxVarSetFlt "*PUI_Stat" fHeadVal 8

	aEquippedItems = ar_Null

end