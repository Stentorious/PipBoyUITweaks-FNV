int iMenuID
int iTileID
int iType
ref rItem
string_var sTileName

begin Function {iMenuID, iTileID, sTileName}

	sv_Destruct sTileName

	; Update tweaks on item select
	if IsFormValid (rItem = GetSelectedItemRef) == 0
		return
	endif

	; Cache equipped item stats and update color
	if eval (iType = rItem.GetType) == 24 || iType == 40
		if eval Goo1.AuxVarGetFlt "*PUI_INI" 0
			CallAfterFrames 0 (CompileScript "PipBoyUITweaks\GetItemStats.gek") 1
		endif
		CallAfterFrames 0 (CompileScript "PipBoyUITweaks\MouseOver.gek") 1 iMenuID
	endif

	; Update Inventory weight/hp color
	if eval iMenuID == 1002
		if eval Goo1.AuxVarGetFlt "*PUI_INI" 4
			CallAfterFrames 0 ({} => SetUIFloatAlt "InventoryMenu\_PUI+WG" (IsPCOverencumbered)) 1
		endif
		if eval Goo1.AuxVarGetFlt "*PUI_INI" 7
			CallAfterFrames 0 ({} => SetUIFloatAlt "StatsMenu\_PUI+HP" (Player.GetHealthPercentage)) 1
		endif
	endif

end